#
# @copyright Copyright (c) 2020 ApertureData Inc.
#

import os
Import('env')

prom_amb_env = env.Clone()
prom_amb_env.Replace(
    CPPPATH = [ 'src', '../../include/util',
                os.getenv('AD_COMM_INCLUDE',     default='../../include'),
                os.getenv('AD_CLIENT_INCLUDE',   default='../../include'),
                os.getenv('PROMETHEUS_CPP_CORE_INCLUDE', default=''),
                os.getenv('PROMETHEUS_CPP_PULL_INCLUDE', default=''),
                os.getenv('NLOHMANN_JSON_INCLUDE', default='/usr/include'),
              ],
    LIBS =    [ 'comm',
                'aperturedb-client',
                'prometheus-cpp-core',
                'prometheus-cpp-pull',
                'glog',
              ],
    LIBPATH = [ '/usr/local/lib/',
                os.getenv('AD_COMM_LIB',         default='../../lib'),
                os.getenv('AD_CLIENT_LIB',       default='../../lib'),
                os.getenv('PROMETHEUS_CPP_LIB',  default=''),
              ],
)

src = [
  'prometheus_ambassador.cc',
  'src/ClientCollector.cc',
  'src/PromConfig.cc',
  'src/PromServer.cc',
  'src/PrintCaughtException.cc',
]

prom_amb_env.Program('prometheus_ambassador', src)

test_env = prom_amb_env.Clone()
test_env.Replace(
    LIBS = prom_amb_env['LIBS'] + ['pthread', 'protobuf', 'gtest'],
    CPPPATH = prom_amb_env['CPPPATH'] + ['../../test', '../../src'],
)

test_src = [
  'test/main.cc',
  'test/ClientCollectorTests.cc',
  'test/PromConfigTests.cc',
]

test_obj = [
  '../../test/Barrier.o',
  'src/ClientCollector.o',
  'src/PromConfig.o',
  'src/PromServer.o',
  'src/PrintCaughtException.o',
  test_src,
]

test_env.Program('prometheus_ambassador_test', test_obj)
